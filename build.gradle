plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "java"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
}

version "0.1"
group "example"

repositories {
    maven {
        url "https://dl.bintray.com/micronaut/core-releases-local"
        //content {
        //    includeGroupByRegex "io\\.micronaut.*"
        //}
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.1.1'
    }
}

dependencies {
    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut:micronaut-validation'
    annotationProcessor 'io.micronaut.configuration:micronaut-openapi'
    compileOnly 'io.micronaut:micronaut-inject-java'
    compile 'io.micronaut:micronaut-http-client'
    compile 'io.micronaut:micronaut-http-server-netty'
    compile 'io.micronaut:micronaut-inject'
    compile 'io.micronaut:micronaut-runtime'
    compile 'io.micronaut:micronaut-management'
    compile 'io.micronaut.configuration:micronaut-hibernate-validator'

    runtime "ch.qos.logback:logback-classic:1.2.3"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
    testCompile 'org.mockito:mockito-junit-jupiter:2.23.4'
    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile 'io.rest-assured:rest-assured:3.2.0'
}

shadowJar {
    mergeServiceFiles()
}

test {
    useJUnitPlatform()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "micro.Application"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

task stage(type: Copy, dependsOn: [assemble]) {
    from 'build/libs/micronaut-0.1-all.jar'
    into project.rootDir
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean {
    doLast {
        project.file('app.jar').delete()
    }
}
